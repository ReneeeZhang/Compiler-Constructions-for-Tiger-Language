structure A = Absyn;

fun group_at_let decs = 
    case decs of
        [] => []
        | A.TypeDec(t1) :: A.TypeDec(t2) :: decs' => group_at_let(A.TypeDec(t1@t2)::decs')
        | A.FunctionDec(f1) :: A.FunctionDec(f2) :: decs' => group_at_let(A.FunctionDec(f1@f2)::decs')
        | x::decs' => x :: group_at_let(decs') 

%%
%term
    EOF 
  | ID of string
  | INT of int | STRING of string 
  | COMMA | COLON | SEMICOLON | LPAREN | RPAREN | LBRACK | RBRACK 
  | LBRACE | RBRACE | DOT 
  | PLUS | MINUS | TIMES | DIVIDE | EQ | NEQ | LT | LE | GT | GE
  | AND | OR | ASSIGN
  | ARRAY | IF | THEN | ELSE | WHILE | FOR | TO | DO | LET | IN | END | OF 
  | BREAK | NIL
  | FUNCTION | VAR | TYPE | UMINUS


%nonassoc THEN TO
%nonassoc ELSE DO
%right OF
%nonassoc ASSIGN
%left OR
%left AND
%nonassoc EQ NEQ LT GT GE LE
%left PLUS MINUS
%left TIMES DIVIDE
%left UMINUS

%nonterm  exp of A.exp
  | program of A.exp
  | cond of A.exp
  | lvalue of A.var
  | loop of A.exp
  | decs of A.dec list
  | expseq of (A.exp * A.pos) list
  | dec of A.dec
  | tydec of A.dec
  | fundec of A.dec
  | vardec of A.dec
  | ty of A.ty
  | tyfields of {name: A.symbol, escape: bool ref, typ: A.symbol, pos: A.pos} list
  | funargs of A.exp list
  | recordargs of (A.symbol * A.exp * A.pos) list
  | expseq_tail of (A.exp * A.pos) list
  | expseq_in_let of (A.exp * A.pos) list
  | expseq_in_let_tail of (A.exp * A.pos) list

%pos int
%verbose
%start program
%eop EOF
%noshift EOF
%name Tiger

%keyword WHILE FOR TO BREAK LET IN END FUNCTION VAR TYPE ARRAY IF THEN ELSE 
        DO OF NIL

%prefer THEN ELSE LPAREN

%value ID ("bogus")
%value INT (1)
%value STRING ("")

%%

program : exp                                            (exp)

decs : dec decs                                          (dec :: decs)
    |                                                    ([])

dec : tydec                                              (tydec)
    | vardec                                             (vardec)
    | fundec                                             (fundec)

tydec : TYPE ID EQ ty                                    (A.TypeDec([{name = Symbol.symbol(ID), ty = ty, pos = IDleft}]))

ty : ID                                                  (A.NameTy(Symbol.symbol(ID), IDleft))
    | LBRACE RBRACE                                      (A.RecordTy([]))
    | LBRACE tyfields RBRACE                             (A.RecordTy(tyfields))
    | ARRAY OF ID                                        (A.ArrayTy(Symbol.symbol(ID), IDleft))

tyfields : ID COLON ID                                   ({name = Symbol.symbol(ID1), 
                                                           escape = ref false, 
                                                           typ = Symbol.symbol(ID2), 
                                                           pos = ID1left} :: [])
    | ID COLON ID COMMA tyfields                         ({name = Symbol.symbol(ID1), 
                                                           escape = ref false, 
                                                           typ = Symbol.symbol(ID2), 
                                                           pos = ID1left} :: tyfields)

vardec : VAR ID ASSIGN exp                               (A.VarDec({name = Symbol.symbol(ID), 
                                                                    escape = ref false, 
                                                                    typ = NONE, 
                                                                    init = exp, 
                                                                    pos = VARleft}))
    | VAR ID COLON ID ASSIGN exp                         (A.VarDec({name = Symbol.symbol(ID1), 
                                                                    escape = ref false, 
                                                                    typ = SOME (Symbol.symbol(ID2), ID2left), 
                                                                    init = exp, 
                                                                    pos = VARleft}))

fundec : FUNCTION ID LPAREN tyfields RPAREN EQ exp       (A.FunctionDec([{name = Symbol.symbol(ID), 
                                                                          params = tyfields, 
                                                                          result = NONE, 
                                                                          body = exp, 
                                                                          pos = FUNCTIONleft}]))
    | FUNCTION ID LPAREN RPAREN EQ exp                   (A.FunctionDec([{name = Symbol.symbol(ID), 
                                                                          params = [], 
                                                                          result = NONE, 
                                                                          body = exp, 
                                                                          pos = FUNCTIONleft}]))
    | FUNCTION ID LPAREN tyfields RPAREN COLON ID EQ exp (A.FunctionDec([{name = Symbol.symbol(ID1), 
                                                                          params = tyfields, 
                                                                          result = SOME(Symbol.symbol(ID2), ID2left), 
                                                                          body = exp, 
                                                                          pos = FUNCTIONleft}]))
    | FUNCTION ID LPAREN RPAREN COLON ID EQ exp          (A.FunctionDec([{name = Symbol.symbol(ID1), 
                                                                          params = [], 
                                                                          result = SOME(Symbol.symbol(ID2), ID2left), 
                                                                          body = exp, 
                                                                          pos = FUNCTIONleft}]))

lvalue : ID                                              (A.SimpleVar(Symbol.symbol(ID), IDleft))
    | ID LBRACK exp RBRACK                               (A.SubscriptVar(A.SimpleVar(Symbol.symbol(ID), IDleft), exp, IDleft))
    | lvalue DOT ID                                      (A.FieldVar(lvalue, Symbol.symbol(ID), lvalueleft))
    | lvalue LBRACK exp RBRACK                           (A.SubscriptVar(lvalue, exp, lvalueleft))

loop : WHILE exp DO exp                                  (A.WhileExp({test = exp1, body = exp2, pos = WHILEleft}))
    |  FOR ID ASSIGN exp TO exp DO exp                   (A.ForExp({var = Symbol.symbol(ID),
                                                                    escape = ref false,
                                                                    lo = exp1,
                                                                    hi = exp2,
                                                                    body = exp3,
                                                                    pos = FORleft}))
        
exp : INT                                                (A.IntExp(INT))
    | STRING                                             (A.StringExp(STRING, STRINGleft))
    | NIL                                                (A.NilExp)
    | BREAK                                              (A.BreakExp(BREAKleft))
    | lvalue ASSIGN exp                                  (A.AssignExp({var=lvalue, exp=exp, pos=lvalueleft}))
    | lvalue                                             (A.VarExp(lvalue))
    | LET decs IN expseq_in_let END                                (A.LetExp({decs = group_at_let(decs), 
                                                                    body = A.SeqExp(expseq_in_let),
                                                                    pos = LETleft}))
    | ID LPAREN RPAREN                                   (A.CallExp({func=Symbol.symbol(ID), args=[], pos=IDleft}))
    | ID LPAREN funargs RPAREN                           (A.CallExp({func=Symbol.symbol(ID), args=funargs, pos=IDleft}))
    | ID LBRACK exp RBRACK OF exp                        (A.ArrayExp({typ=Symbol.symbol(ID), size=exp1, init=exp2, pos=IDleft}))
    | ID LBRACE RBRACE                                   (A.RecordExp({fields=[], typ=Symbol.symbol(ID), pos=IDleft}))
    | ID LBRACE recordargs RBRACE                        (A.RecordExp({fields=recordargs, typ=Symbol.symbol(ID), pos=IDleft}))
    | cond                                               (cond)
    | loop                                               (loop)
    | exp AND exp                                        (A.IfExp({test=exp1, then'=exp2, else'=SOME(A.IntExp(0)), pos=exp1left}))
    | exp OR exp                                         (A.IfExp({test=exp1, then'=A.IntExp(1), else'=SOME(exp2), pos=exp1left}))
    | exp EQ exp                                         (A.OpExp({left=exp1, oper=A.EqOp, right=exp2, pos=exp1left}))
    | exp NEQ exp                                        (A.OpExp({left=exp1, oper=A.NeqOp, right=exp2, pos=exp1left}))
    | exp LT exp                                         (A.OpExp({left=exp1, oper=A.LtOp, right=exp2, pos=exp1left}))
    | exp GT exp                                         (A.OpExp({left=exp1, oper=A.GtOp, right=exp2, pos=exp1left}))
    | exp LE exp                                         (A.OpExp({left=exp1, oper=A.LeOp, right=exp2, pos=exp1left}))
    | exp GE exp                                         (A.OpExp({left=exp1, oper=A.GeOp, right=exp2, pos=exp1left}))
    | exp PLUS exp                                       (A.OpExp({left=exp1, oper=A.PlusOp, right=exp2, pos=exp1left}))
    | exp MINUS exp                                      (A.OpExp({left=exp1, oper=A.MinusOp, right=exp2, pos=exp1left}))
    | exp TIMES exp                                      (A.OpExp({left=exp1, oper=A.TimesOp, right=exp2, pos=exp1left}))
    | exp DIVIDE exp                                     (A.OpExp({left=exp1, oper=A.DivideOp, right=exp2, pos=exp1left}))
    | MINUS exp         %prec UMINUS                     (A.OpExp({left=A.IntExp(0), oper=A.MinusOp, right=exp, pos=MINUSleft}))
    | LPAREN expseq RPAREN                               (A.SeqExp(expseq))
    | LPAREN exp RPAREN                                  (exp)
    | LPAREN RPAREN                                      (A.SeqExp([]))

recordargs : ID EQ exp                                   ([(Symbol.symbol(ID), exp, IDleft)])
    | ID EQ exp COMMA recordargs                         ((Symbol.symbol(ID), exp, IDleft) :: recordargs)

funargs : exp                                            ([exp])
    | exp COMMA funargs	                                 (exp :: funargs)

cond : IF exp THEN exp ELSE exp                          (A.IfExp({test = exp1,
                                                                   then' = exp2,
                                                                   else' = SOME(exp3),
                                                                   pos = IFleft}))
    | IF exp THEN exp                                    (A.IfExp({test = exp1,
                                                                   then' = exp2,
                                                                   else' = NONE,
                                                                   pos = IFleft}))

expseq_tail : exp SEMICOLON expseq_tail                  ((exp, expleft) :: expseq_tail)
    | exp                                                ([(exp, expleft)])

expseq : exp SEMICOLON expseq_tail                       ((exp, expleft) :: expseq_tail)

expseq_in_let : exp expseq_in_let_tail                   ((exp, expleft) :: expseq_in_let_tail)
    |                                                    ([])

expseq_in_let_tail : SEMICOLON exp expseq_in_let_tail    ((exp, expleft) :: expseq_in_let_tail)
    |                                                    ([])